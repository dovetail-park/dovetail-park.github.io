---
title: '[VBA] 01.로그인 폼 만들기'
layout: post
subtitle: 개요
date: '2021-02-11 11:45:51 +0900'
categories: 정보기술
#tags:
comments: false
related_posts:
  - category/_posts/정보기술/2021-02-13-VBA_2.md
published: true
---

# 로그인 폼 만들기

## 1. 개요
---
2020년 지방기능경기대회 제4과제 스프레드시트의 로그인 폼 만들기에서 다음과 같은 조건을
만족하는 로그인 폼을 만들어 본다.
- 조건
    - [1. PW란은 비밀번호가 ●로 표시되도록 하시오.(그림1)](#1.개요)
    - [2. Login 버튼을 클릭시 아래와 같은 오류를 제어하시오.](#2.소스코드)
    - [3. 공백이 있을 경우(그림2)](#3.무제)
    - [4. ID 또는 PW가 일치하지 않았을 경우(그림3)](#4.무제2)
    - [5. 로그인 성공(그림4)](#5.무제2)

![그림1](../../../../assets/img/정보기술/VBA_1_그림1.jpg "그림1")
[그림1]

![그림2](../../../../assets/img/정보기술/VBA_1_그림2.jpg "그림2")
[그림2]

![그림3](../../../../assets/img/정보기술/VBA_1_그림3.jpg "그림3")
[그림3]

![그림4](../../../../assets/img/정보기술/VBA_1_그림4.jpg "그림4")
[그림4]

## 2. 소스 코드
---
```vb
Option Explicit

Private Sub txtID_Enter()

    If Me.txtID.Value = "아이디" Then
    
        Me.txtID.Value = ""
        Me.txtID.ForeColor = RGB(0, 0, 0)
        
    End If

End Sub

Private Sub txtID_Exit(ByVal Cancel As MSForms.ReturnBoolean)

    If Me.txtID.Value = "" Then
    
        Me.txtID.Value = "아이디"
        Me.txtID.ForeColor = RGB(128, 128, 128)
        
    End If

End Sub

Private Sub txtPW_Enter()

    If Me.txtPW.Value = "비밀번호" Then
    
        Me.txtPW.Value = ""
        Me.txtPW.ForeColor = RGB(0, 0, 0)
        Me.txtPW.PasswordChar = "●"
        
    End If

End Sub

Private Sub txtPW_Exit(ByVal Cancel As MSForms.ReturnBoolean)

    If Me.txtPW.Value = "" Then
    
        Me.txtPW.Value = "비밀번호"
        Me.txtPW.ForeColor = RGB(128, 128, 128)
        Me.txtPW.PasswordChar = ""
        
    End If

End Sub

Private Sub btnLogin_Click()

    Login_Verification

End Sub

Sub Login_Verification()

    Dim EndRow As Long
    Dim i As Integer
    
    If Me.txtID.Value = "아이디" Then MsgBox "누락된 항목이 있습니다.": Exit Sub
    If Me.txtPW.Value = "비밀번호" Then MsgBox "누락된 항목이 있습니다.": Exit Sub
    
    With shtUser
        EndRow = .Cells(.Rows.Count, 2).End(xlUp).Row
        For i = 2 To EndRow
            If .Cells(i, 2).Value = Me.txtID.Value And .Cells(i, 3) = Me.txtPW.Value Then
                MsgBox .Cells(i, 4).Value & "님 로그인 하셨습니다."
                Unload Me
                Exit Sub
            End If
    Next
    
    MsgBox "아이디 혹은 비밀번호가 일치하지 않습니다."
    
    End With
        
End Sub
```
